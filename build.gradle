// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        version_name = '3.2.0'
    }
    repositories {
        mavenCentral()
        google()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'

        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.21.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        mavenLocal()
    }
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublishing {
            publishToMavenCentral("S01")
            signAllPublications()
        }
        /*signing {
            useGpgCmd()
        }*/
    }
}

subprojects {
    afterEvaluate {
        if (it.findProperty('publishLibrary') ?: false) {
            plugins.apply('com.vanniktech.maven.publish')
            version = version_name
            println("${it.displayName}: ${group}:${it.name}:${version}")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    GROUP = "dev.rikka.hidden"

    POM_INCEPTION_YEAR = "2022"
    POM_PACKAGING = "aar"

    POM_URL = "https://github.com/RikkaW/HiddenApi"
    POM_SCM_URL = "https://github.com/RikkaW/HiddenApi"
    POM_SCM_CONNECTION = "scm:git@github.com:RikkaW/HiddenApi.git"
    POM_SCM_DEV_CONNECTION = "scm:git@github.com:RikkaW/HiddenApi.git"

    POM_LICENCE_NAME = "MIT License"
    POM_LICENCE_URL = "https://github.com/RikkaW/HiddenApi/blob/master/LICENSE"
    POM_LICENCE_DIST = "repo"

    POM_DEVELOPER_ID = "rikka"
    POM_DEVELOPER_NAME = "Rikka"
    POM_DEVELOPER_URL = "https://github.com/RikkaW"
}
